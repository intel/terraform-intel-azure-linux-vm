
<p align="center">
   <img src="https://github.com/intel/terraform-intel-azure-linux-vm/blob/main/images/logo-classicblue-800px.png?raw=true" alt="Intel Logo" width="250"/>
</p>

# Intel® Optimized Cloud Modules for Terraform

© Copyright 2025, Intel Corporation

## Terraform Intel Azure VM - Linux VM Creating Multiple Disks using Intel Cloud Optimzier(ICO) by Densify recommendations

<p align="center">
  <img src="https://github.com/intel/terraform-intel-azure-linux-vm/blob/main/images/azure-vm-ico.png?raw=true" alt="Intel + Densify Logo" width="250"/>
</p>

This example creates multiple disks on an Azure virtual machine on Intel® 5th Generation Xeon® Scalable Emerald Rapids CPUs on Linux Operating System using recommended instance from Intel Cloud Optimizer by Densify. The virtual machine is created on an Intel Emerald Rapids Standard_D4ds_v6 by as an example of recommendation by ICO by Densify.

Intel® Cloud Optimizer is a collaboration between Densify and Intel targeted at getting you the most from your cloud investment. 

Intel Cloud Optimizer by Densify helps customers optimize their cloud investments and ensure optimal performance for every workload.

Using this example requires a "densify_recommndations.auto.tfvars" file. You are expected to generate this file so this is a sample file only. 

Intel Cloud Optimizer by Densify is a commercial product. With Intel® Cloud Optimizer, Intel funds the use of Densify for qualifying enterprises for 12 months. For full details of the Intel Cloud Optimizer by Densify offer please see: [INTEL CLOUD OPTIMIZER by DENSIFY](https://www.densify.com/product/intel/)

As you configure your application's environment, choose the configurations for your infrastructure that matches your application's requirements.

In this example, the virtual machine is using a preconfigured network interface, subnet, and resource group. The tags Name, Owner and Duration are added to the virtual machine when it is created.

## Usage

** See examples folder for code ./examples/azure-linux-multi-disks/main.tf **

variables.tf
```hcl
variable "admin_password" {
  description = "The Password which should be used for the local-administrator on this virtual machine"
  type        = string
  sensitive   = true
  validation {
    condition     = length(var.admin_password) >= 8
    error_message = "The admin_password value must be at least 8 characters in length"
  }
}

#ICO by Densify Name of the system.
variable "name" {
  default = "test"
}

variable "densify_recommendations" {
  type = map(map(string))
  #sample map of map this would be generated by densify provided as a .auto.tfvars file that would have all the systems and info listed. 
  #In the file there would be multiple systems defined in this case it just has 1 called test. 
  #To see how it would work you can change the approvalType from all to na. As all assumes you have approved all changes and na would be used to say haven't approved the change and just want to make the system self-aware. 
  default = { 
    test = {
      recommendedType = "Standard_D4ds_v6"
      currentType = "Standard_D4ds_v2"
      approvalType = "all"
      savingsEstimate = "31.43"
      predictedUptime = "83.4"
      reservedInstanceCoverage = "no"
    }
  }
}

# ICO by Densify Defaults this is used for fallback if the system name isn't found in the densify_recommendations. 
# This shouldn't be used in most cases likely use would be if you were to create a new system that hasn't been analyzed by Densify yet.
variable "densify_fallback"{
  type = map(string)
  default = {
      	recommendedType = "Standard_D4ds_v6"
     	currentType = "Standard_D4ds_v2"
	approvalType = "all"
	savingsEstimate = "0"
	predictedUptime = "0"
	reservedInstanceCoverage = "no"
  }
}
```

main.tf
```hcl
# Example of how to pass variable for database password:
# terraform apply -var="db_password=..."
# Environment variables can also be used https://www.terraform.io/language/values/variables#environment-variables
# Resource azurerm_managed_disk requires a preconfigured resource group in Azure
# Resource azurerm_linux_virtual_machine requires a preconfigured resource group, virtual network, and subnet in Azure

# Initialize Densify Module that will parse the densify_recommendations.auto.tfvars recommendation file
module "densify" {
  source  = "densify-dev/optimization-as-code/null"
  densify_recommendations = var.densify_recommendations
  densify_fallback        = var.densify_fallback
  densify_unique_id       = var.name
}

resource "azurerm_managed_disk" "managed_disk" {
  name                 = "managed_disk_name"
  resource_group_name  = "terraform-testing-rg"
  storage_account_type = "Standard_LRS"
  location             = "eastus"
  create_option        = "Empty"
  disk_size_gb         = 8
  tags = {
    "owner"    = "user@company.com"
    "duration" = "1"
  }
}

resource "azurerm_virtual_machine_data_disk_attachment" "disk_attachment" {
  managed_disk_id    = azurerm_managed_disk.managed_disk.id
  virtual_machine_id = module.azurerm_linux_virtual_machine.virtual_machine_id
  lun                = 10
  caching            = "ReadWrite"
}

module "azurerm_linux_virtual_machine" {
  source                              = "intel/azure-linux-vm/intel"
  azurerm_resource_group_name         = "terraform-testing-rg"
  azurerm_virtual_network_name        = "vm-vnet1"
  virtual_network_resource_group_name = "terraform-testing-rg"
  azurerm_subnet_name                 = "default"
  admin_password                      = var.admin_password

  # ICO by Densify normal way of sizing an instance by hardcoding the size.
  #virtual_machine_size = "Standard_D4ds_v4"

  # ICO by Densify new self-optimizing instance type from Densify
  virtual_machine_size = module.densify.instance_type
  source_image_reference = {
    "offer"     = "0001-com-ubuntu-server-jammy"
    "sku"       = "22_04-lts-gen2"
    "publisher" = "Canonical"
    "version"   = "latest"
  }
  tags = {
    "owner"    = "user@company.com"
    "duration" = "1"
    # ICO by Densify tag instance to make it Self-Aware these tags are optional and can set as few or as many as you like.
    Name = var.name
    Current-instance-type = module.densify.current_type
    Densify-optimal-instance-type = module.densify.recommended_type
    Densify-potential-monthly-savings = module.densify.savings_estimate
    Densify-predicted-uptime = module.densify.predicted_uptime
    #Should match the densify_unique_id value as this is how Densify references the system as unique
    Densify-Unique-ID = var.name
  }
}


```

Run Terraform

```hcl
terraform init  
terraform plan
terraform apply

```

Note that this example may create resources. Run `terraform destroy` when you don't need these resources anymore.

## Considerations  

```hcl
When admin_password is specified disable_password_authentication must be set to false

Either admin_password or admin_ssh_key must be specified

Only Managed Disks are supported via this separate resource, Unmanaged Disks can be attached using the storage_data_disk block in the azurerm_virtual_machine resource.

```